#MIN_CONFIG

yzis_version=`cat $srcdir/VERSION`

AM_INIT_AUTOMAKE(yzis, $yzis_version)

dnl These are common macros that you might or might not want to use

dnl Checks for header files.
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_HEADER_TIME
AC_CHECK_HEADERS(fcntl.h sys/time.h unistd.h stdlib.h paths.h)
AC_CHECK_FUNCS(usleep)
AC_PATH_LUA
AC_CHECK_HEADER(magic.h)
if test "$ac_cv_header_magic_h" = "no"; then
	AC_MSG_ERROR(magic.h not found. Please install libmagic headers.)
fi
AC_CHECK_LIB(magic, magic_open,[have_libmagic=yes],[have_libmagic=no])
if test "$have_libmagic" = "no"; then
	AC_MSG_ERROR(libmagic not found. Please install libmagic.)
fi
AC_CHECK_HEADER(libps/pslib.h)
if test "$ac_cv_header_libps_pslib_h" = "no"; then
	AC_MSG_WARN(libps.h not found. pslib will not be used.)
fi
AC_CHECK_LIB(ps, PS_shutdown,[have_libps=yes],[have_libps=no])
if test "$have_libps" = "no"; then
	AC_MSG_WARN(libps not found. pslib will not be used.)
fi
if test "$have_libps" = "yes" -a "$ac_cv_header_libps_pslib_h" = "yes"; then
	AC_DEFINE(HAVE_LIBPS,1,[whether libps is present or not])
	PS_LIBS="-lps"
fi

yzis_cv_curses=/usr
AC_ARG_WITH(curses, AC_HELP_STRING([--with-curses=DIR],[where ncurses is installed]),
			[if test $withval != yes; then
			 yzis_cv_curses=$withval
		 fi
		 if test x$yzis_cv_curses != x/usr; then
			 LDFLAGS="-L${yzis_cv_curses}/lib $LDFLAGS"
			 CXXFLAGS="$CPPFLAGS -I${yzis_cv_curses}/include"
			 YZIS_INCLUDES="$YZIS_INCLUDES -I${yzis_cv_curses}/include"
		 fi])

cf_ncurses="ncurses"
AC_CHECK_LIB(ncurses, waddnwstr, [cf_ncurses="ncurses"; break],[have_ncurses="no"])
AC_CHECK_LIB(ncursesw, waddnwstr, [cf_ncurses="ncursesw"; break],[have_ncursesw="no"])

AC_CHECK_LIB($cf_ncurses, initscr,
			 [YZISLIBS="$YZISLIBS -l$cf_ncurses"
			  if test x$yzis_cv_curses = x/usr -a -d /usr/include/ncursesw; then
				  YZIS_INCLUDES="$YZIS_INCLUDES -I/usr/include/ncursesw"
			  else 
				  if test x$yzis_cv_curses = x/usr -a -d /usr/include/ncurses; then
					  YZIS_INCLUDES="$YZIS_INCLUDES -I/usr/include/ncurses"
				  fi
			  fi
			  if test "$cf_ncurses" = ncursesw; then
				  AC_CHECK_HEADERS(ncursesw/ncurses.h,[cf_cv_ncurses_header="ncursesw/ncurses.h"])
			  else
				  AC_CHECK_HEADERS(ncurses.h,[cf_cv_ncurses_header="ncurses.h"])
			  fi], [CF_CURSES_LIBS])
if test "$have_ncurses" = "no" -a "$have_ncursesw" = "no" ; then
	AC_MSG_WARN(Ncurses libraries with wide characters support not found, nyzis will not be compiled.)
	DO_NOT_COMPILE="$DO_NOT_COMPILE nyzis"
fi
if test "$cf_cv_ncurses_header" = ""; then
	AC_MSG_WARN(Ncurses header not found, nyzis will not be compiled.)
	DO_NOT_COMPILE="$DO_NOT_COMPILE nyzis"
fi

ifdef([AM_PATH_CPPUNIT],
       [AM_PATH_CPPUNIT(1.8.0)],
       )
AM_CONDITIONAL(HAVE_CPPUNIT, test "$CPPUNIT_LIBS")

KDE_INIT_DOXYGEN([The Yzis API Reference], [Version $VERSION])

AC_ARG_ENABLE(kyzis, AC_HELP_STRING([--disable-kyzis],[disable Kyzis compilation.]),, [enable_kyzis="yes"])
AC_ARG_ENABLE(nyzis, AC_HELP_STRING([--disable-nyzis],[disable Nyzis compilation.]),, [enable_nyzis="yes"])
AC_ARG_WITH(qt4-dir, AC_HELP_STRING([--with-qt4-dir=DIR],[Enable Qt4 support -- EXPERIMENTAL --]),[enable_qt4="yes"; QT_INCLUDES="-I$withval/include/Qt"; QT_LIBS="-L$withval/lib"; QTDIR="$withval"],)

if test "$enable_qt4" != "yes"; then
	YZIS_INCLUDES="$YZIS_INCLUDES $QT_INCLUDES"
	YZIS_SUBDIRS="$YZIS_SUBDIRS libqtyzis"
	NYZIS_DEP="libyzis/libqtyzis/libqtyzis.la"
else
	YZIS_INCLUDES="$YZIS_INCLUDES $QT_INCLUDES"
	YZIS_SUBDIRS="$YZIS_SUBDIRS libqt4yzis"
	NYZIS_DEP="libyzis/libqt4yzis/libqt4yzis.la"
fi

if test "$have_kde" != "yes" -o "$enable_kyzis" != "yes" -o "$enable_qt4" = "yes"; then
	DO_NOT_COMPILE="$DO_NOT_COMPILE kyzis"
fi
if test "$enable_nyzis" != "yes"; then
	DO_NOT_COMPILE="$DO_NOT_COMPILE nyzis"
fi

DO_NOT_COMPILE="$DO_NOT_COMPILE tests"

AC_SUBST(YZIS_SUBDIRS)
AC_SUBST(YZIS_INCLUDES)
AC_SUBST(NYZIS_DEP)
AC_SUBST(PS_LIBS)
AC_SUBST(YZISLIBS)
AC_CONFIG_FILES([ libyzis/translator.h ])

