#MIN_CONFIG

AM_INIT_AUTOMAKE(yzis,`cat $srcdir/VERSION`)

dnl These are common macros that you might or might not want to use

dnl Checks for header files.
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_HEADER_TIME
AC_CHECK_HEADERS(fcntl.h sys/time.h unistd.h stdlib.h paths.h)
AC_CHECK_FUNCS(usleep)
dnl KDE_ENABLE_HIDDEN_VISIBILITY

AC_PATH_LUA
AC_PATH_LIBMAGIC

case $host in *-*-freebsd* | *-*-netbsd* | *-*-openbsd*)
	AC_PATH_LIBGETTEXT
	;;
esac

AC_ARG_ENABLE(pslib, AC_HELP_STRING([--disable-pslib],[disable Pslib-support.]),, [enable_pslib="no"])
if test "$enable_pslib" == "yes"; then
	AC_CHECK_HEADER(libps/pslib.h)
	if test "x$ac_cv_header_libps_pslib_h" = "xno"; then
		AC_MSG_WARN(libps.h not found. pslib will not be used.)
	fi
	AC_CHECK_LIB(ps, PS_shutdown,[have_libps=yes],[have_libps=no])
	if test "x$have_libps" = "xno"; then
		AC_MSG_WARN(libps not found. pslib will not be used.)
	fi
	if test "x$have_libps" = "xyes" -a "x$ac_cv_header_libps_pslib_h" = "xyes"; then
		AC_DEFINE(HAVE_LIBPS,1,[whether libps is present or not])
		PS_LIBS="-lps"
	fi
else
	have_libps="no"
fi


dnl
dnl curses check
dnl
yzis_cv_curses=/usr
AC_ARG_WITH(curses-dir, AC_HELP_STRING([--with-curses-dir=DIR],[where ncurses is installed]),
		[
		yzis_cv_curses="$withval"
		CURSES="$withval"
		CURSES_INCLUDES="-I$withval/include -I$withval/include/ncurses"
		CURSES_LIBS="-L$withval/lib"
		])
ac_save_CFLAGS="$CFLAGS"
ac_save_CPPFLAGS="$CPPFLAGS"
ac_save_LDFLAGS="$LDFLAGS"
CFLAGS="$CURSES_INCLUDES $CFLAGS"
CPPFLAGS="$CURSES_INCLUDES $CPPFLAGS"
LDFLAGS="$CURSES_LIBS $LDFLAGS"

cf_ncurses="ncurses"
AC_CHECK_LIB(ncurses, waddnwstr, [have_ncurses="yes"],[have_ncurses="no"])
AC_CHECK_LIB(ncurses, addch, [have_basic_ncurses="yes"],[have_basic_ncurses="no"])
AC_CHECK_LIB(ncursesw, waddnwstr, [have_ncursesw="yes"],[have_ncursesw="no"])

if test "x$have_ncurses" = "xyes" -o "x$have_basic_ncurses" = "xyes" ; then
	cf_ncurses="ncurses"
fi
if test "x$have_ncursesw" = "xyes"; then
	cf_ncurses="ncursesw"
fi

AC_CHECK_LIB($cf_ncurses, initscr,
			 [YZISLIBS="$YZISLIBS -l$cf_ncurses"
			  if test -d ${yzis_cv_curses}/include/ncursesw; then
				  YZIS_INCLUDES="$YZIS_INCLUDES -I${yzis_cv_curses}/include/ncursesw"
			  else 
				  if test -d ${yzis_cv_curses}/include/ncurses; then
					  YZIS_INCLUDES="$YZIS_INCLUDES -I${yzis_cv_curses}/include/ncurses"
				  fi
			  fi
			  AC_CHECK_HEADERS(ncurses.h,[cf_cv_ncurses_header="ncurses.h"])
			  AC_CHECK_HEADERS(ncursesw/ncurses.h,[cf_cv_ncurses_header="ncursesw.h"])
			  ], [CF_CURSES_LIBS])
if test "x$have_ncurses" = "xno" -a "x$have_ncursesw" = "xno" -a "x$have_basic_ncurses" = "xyes"; then
	AC_MSG_WARN(WARNING Ncurses libraries with wide characters support not found, nyzis will NOT be compiled. Use a ncurses compiled with unicode support !)
	DO_NOT_COMPILE="$DO_NOT_COMPILE nyzis"
else 
	if test "x$have_ncurses" = "xno" -a "x$have_ncursesw" = "xno" -a "x$have_basic_ncurses" = "xno"; then
		AC_MSG_WARN(Ncurses libraries was not found, nyzis will not be compiled.)
		DO_NOT_COMPILE="$DO_NOT_COMPILE nyzis"
	fi
fi
if test "x$cf_cv_ncurses_header" = "x"; then
	AC_MSG_WARN(Ncurses header not found, nyzis will not be compiled.)
	DO_NOT_COMPILE="$DO_NOT_COMPILE nyzis"
fi
CFLAGS="$ac_save_CFLAGS"
CPPFLAGS="$ac_save_CPPFLAGS"
LDFLAGS="$ac_save_LDFLAGS"

AC_SUBST(CURSES)
AC_SUBST(CURSES_INCLUDES)
AC_SUBST(CURSES_LIBS)
dnl
dnl end curses check
dnl

ifdef([AM_PATH_CPPUNIT],
       [AM_PATH_CPPUNIT(1.10.0)],
       )
AM_CONDITIONAL(HAVE_CPPUNIT, test "$CPPUNIT_LIBS")

KDE_INIT_DOXYGEN([The Yzis API Reference], [Version $VERSION])

AC_ARG_ENABLE(kyzis, AC_HELP_STRING([--disable-kyzis],[disable Kyzis compilation.]),, [enable_kyzis="yes"])
AC_ARG_ENABLE(nyzis, AC_HELP_STRING([--disable-nyzis],[disable Nyzis compilation.]),, [enable_nyzis="yes"])
AC_ARG_WITH(qt4-dir, AC_HELP_STRING([--with-qt4-dir=DIR],[Enable Qt4 support -- EXPERIMENTAL --]),[enable_qt4="yes"; QT_INCLUDES="-I$withval/include -I$withval/include/QtCore  -I$withval/include/QtXml -I$withval/include/QtGui"; QT_LIBS="-L$withval/lib"; QTDIR="$withval"],)

if test "x$enable_qt4" != "xyes"; then
	YZIS_QT3="libqtyzis"
	NYZIS_DEP="libyzis/libqtyzis/libqtyzis.la"
	YZIS_INCLUDES="$YZIS_INCLUDES $all_includes"
else
	YZIS_QT4="libqt4yzis"
	NYZIS_DEP="libyzis/libqt4yzis/libqt4yzis.la"
	YZIS_INCLUDES="$QT_INCLUDES $YZIS_INCLUDES"
	MOC="$QTDIR/bin/moc"
	AC_SUBST(MOC)
fi
AM_CONDITIONAL(use_libqt, test -n "$YZIS_QT3")
AM_CONDITIONAL(use_libqt4, test -n "$YZIS_QT4")


if test "x$have_kde" != "xyes" -o "x$enable_kyzis" != "xyes" -o "x$enable_qt4" = "xyes"; then
	DO_NOT_COMPILE="$DO_NOT_COMPILE kyzis kqcommon"
fi
if test "x$enable_nyzis" != "xyes"; then
	DO_NOT_COMPILE="$DO_NOT_COMPILE nyzis"
fi

DO_NOT_COMPILE="$DO_NOT_COMPILE tests gyzis"

AC_SUBST(YZIS_QT3)
AC_SUBST(YZIS_QT4)
AC_SUBST(YZIS_INCLUDES)
AC_SUBST(NYZIS_DEP)
AC_SUBST(PS_LIBS)
AC_SUBST(YZISLIBS)
AC_SUBST(QT_LIBS)
AC_CONFIG_FILES( [ libyzis/translator.h ] )

